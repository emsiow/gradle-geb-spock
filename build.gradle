plugins {
    id "idea"
    id "groovy"
}

wrapper {
    gradleVersion = "4.7"
}

ext {
    drivers = ["chrome", "chromeHeadless"]
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.gebish:geb-spock:2.1"
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
    testCompile "org.codehaus.groovy:groovy-all:2.4.15"

    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:3.11.0"

    testCompile "io.github.bonigarcia:webdrivermanager:2.2.1"

    testCompile "com.github.groovy-wslite:groovy-wslite:1.1.3"
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
        systemProperty "env", System.getProperty("env")

        include "**/examples/**"
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs "-XX:MaxMetaspaceSize=128m"
    testLogging {
        exceptionFormat = "full"
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = "256m"
}
